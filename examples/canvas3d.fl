
-- Notes:
--
-- * All angles are in turns
-- * There is no support (currently) for shadow-casting
-- * Similarly, there is no support for reflections (beyond light sources and
--   material specular reflections)
-- * Point and spot light sources have inverse-squared fall-off in intensity
-- * Material shine is controlled PBR `roughness` in the range (0,1]
-- * `transparency` can be used to create partially or fully transparent
--   materials which *do not* refract light, but *do* have specular reflections

%pragma tempo 60

let SIZE=1920;1080
    -- This offset chosen to produce an interesting static image:
    t=20.5+beat

!window size=SIZE
    !record filename=OUTPUT
        !bloom radius=10
            !canvas3d samples=4 viewpoint=0;150;-750 focus=0;100;0 up=0;1;0 fov=60/360 near=1 far=1500 \
                      fog_color=0;0;0.02 fog_min=0 fog_max=1500
                -- Ambient (directionless) light source:
                !light color=0;0;0.1
                -- Directional light source:
                !light color=0;0.2;0 direction=0;-1;0
                -- Spotlight:
                !light color=1M;0;0 position=1050;1000;0 direction=-1;-1;0 outer=10/360
                -- Ground and pedestal:
                !material color=0.1 roughness=0.4
                    !cylinder size=2000;2000;1 rotation=0.25;0;0
                    !cylinder size=100;100;20 rotation=0.25;0;0
                !transform translate=0;100;0
                    -- Spinning semi-transparent cube:
                    !material color=0.1 roughness=0.3 transparency=2/3
                        !transform rotate=t/11;0;t/7
                            !box size=90 invert=true -- draw back faces...
                            !box size=100            -- ...and front
                    -- Rotating sphere lights:
                    let n=6
                    for i in ..n
                        !transform rotate_x=sin(t/25)*0.02 rotate_y=t/9+i/n translate=150+150*sine(t/16);0;0
                            let c=hsv(i/n;1;1)
                            -- Use `emissive` to make the spheres glow:
                            !material emissive=c*2
                                !sphere size=15
                            -- Place a point light source inside each sphere:
                            !light color=c*5k position=0 radius=15
