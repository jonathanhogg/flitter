--
-- Adjust the number of faces of the sphere by changing SEGMENTS to see how
-- this affects the triangularization and texture mapping.
--
-- SEGMENTS represents the number of edges around the equator and around the
-- 0/0.5 and 0.25/0.75 lines of longitude. These lines split the sphere into
-- eight spherical triangle octants, which are each subdivided into
-- (SEGMENTS // 4)**2 triangular faces. The model is flat shaded to make these
-- triangles more apparent.
--
-- As the poles are formed from 4 triangles, triangular sections of the texture
-- at the very top and bottom are omitted from the final mapping.
--
-- Texture wrapping begins at the X axis and proceeds (anticlockwise) around
-- the Z axis. Maps commonly have the 0 longitude point in the middle of the
-- image, which will end up aligned to the negative X axis. We add a 0.25
-- rotation around the Z axis to align the 0 longitude point with the viewer
-- (negative Y axis).
--
-- This example also uses quaternions to describe spherical coordinates and to
-- do spherical interpolation (slerp) between them.
--

%pragma tempo 60

import axes from 'common/axes.fl'

let SEGMENTS=20

func rotate_to(lat, lon)
    -- Construct a quaternion to represent a location by rotating around the
    -- Z axis by the longitude and then around the X axis by the latitude
    qmul(quaternion(1;0;0, lat/360), quaternion(0;0;1, lon/360))

let t=0.5+beat/10
    london=rotate_to(51.50335, 0.07940)
    sydney=rotate_to(-33.87271, -151.20569)

!window size=(1920;1080)
    !shader id=:map size=2400;1200 hidden=true composite=:difference
        !image id=:earth filename='assets/earth.jpg'
        !canvas id=:canvas antialias=false
            for x in (..2400|50)+0.5
                !line points=x;0;x;1200
            for y in (..1200|50)+0.5
                !line points=0;y;2400;y
            !stroke color=1 stroke_width=1
    !record filename=OUTPUT
        !canvas3d viewpoint=0;-600;0 focus=0;0;25 up=0;0;1 fov_ref=:narrow
            !light color=1 direction=-1;2;-2
            !light color=1 direction=2;1;2
            !transform rotate=slerp(t, london, sydney) rotate_z=0.25
                !material roughness=0.4 texture_id=:map translucency=150
                    !sphere size=350 flat=true segments=SEGMENTS
                @axes size=450
