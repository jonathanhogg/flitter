
%pragma tempo 60

-- flitter examples/physics.fl --verbose --lockstep --fps=60 --define RECORD=1

import bloom_filter from 'bloom.fl'

let SIZE=(1080;1080) * (1 if RECORD else 2)
    CYCLE=beat/30
    NBUBBLES=500
    RADIUS=200+300*sine(0.5+CYCLE*2)
    ROT=sine(CYCLE)
    NOISE_T=CYCLE*10
    NOISE_SCALE=1/5
    MIN_BUBBLE_RADIUS=5
    MAX_BUBBLE_RADIUS=50
    BRIGHTNESS=(1+noise(:brightness, i*NOISE_SCALE, NOISE_T)) for i in ..NBUBBLES
    START=(uniform(:start)[..3*NBUBBLES]-0.5)*RADIUS*2

let bubble_radii=MIN_BUBBLE_RADIUS+(1+noise(:radii, i*NOISE_SCALE, NOISE_T))/2*(MAX_BUBBLE_RADIUS-MIN_BUBBLE_RADIUS) for i in ..NBUBBLES

!physics dimensions=3 state=:bubble resolution=1/60 --speed_of_light=10000
    !anchor id=:centre position=0;0;0
    for i in ..NBUBBLES
        let r=bubble_radii[i]
        !particle id=i initial=START[i*3..(i+1)*3] mass=r*r/100 radius=r charge=r-(MAX_BUBBLE_RADIUS-MIN_BUBBLE_RADIUS)/2
        !distance fixed=RADIUS-r/2 from=i to=:centre coefficient=1000
    !drag coefficient=0.2
    !collision coefficient=1000
    !electrostatic coefficient=100 max_distance=MAX_BUBBLE_RADIUS*5
    -- !constant direction=0;0;-1 coefficient=1000
    -- !gravity coefficient=1000

!window size=SIZE linear=true colorbits=16
    !record filename=('blobs.mp4' if RECORD and CYCLE > 1) codec=:hevc crf=20 limit=30
        @bloom_filter radius=hypot(SIZE)/150
            !canvas3d viewpoint=0;0;1200 focus=0 near=1 far=2000 fov=60/360 samples=4
                !light color=2;0;0
                !light color=-1.9;0;0 direction=0;0;-1
                !transform rotate_x=ROT/2 rotate_y=ROT/2
                    !sphere subdivisions=3 color=0.05 size=RADIUS position=$(:bubble;:centre) transparency=0.1
                    for i in ..NBUBBLES
                        !sphere subdivisions=3 position=$(:bubble;i) color=BRIGHTNESS[i] size=bubble_radii[i]
