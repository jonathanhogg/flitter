
-- To record a 30 second video, run:
-- flitter examples/physics.fl --verbose --lockstep --define RECORD=1

import bloom_filter from 'bloom.fl'

let SIZE=(1080;1080) * (1 if RECORD else 2)
    CYCLE=beat/30
    NBUBBLES=500
    RADIUS=250+250*sine(0.5+CYCLE*2)
    ROT=sine(CYCLE)
    NOISE_T=CYCLE*10
    NOISE_SCALE=1/5
    MIN_BUBBLE_RADIUS=5
    MAX_BUBBLE_RADIUS=50
    START=(beta(:start)[..3*NBUBBLES]-0.5)*RADIUS*2

let bubble_radii=MIN_BUBBLE_RADIUS+(1+noise(:radii, i*NOISE_SCALE, NOISE_T))/2*(MAX_BUBBLE_RADIUS-MIN_BUBBLE_RADIUS) for i in ..NBUBBLES
    bubble_brightness=(1+noise(:brightness, i*NOISE_SCALE, NOISE_T)) for i in ..NBUBBLES

!physics dimensions=3 state=:bubble
    let ease_in=linear(CYCLE)
    !anchor id=:centre position=0;0;0
    for i in ..NBUBBLES
        let r=bubble_radii[i]
        !particle id=i position=START[i*3..(i+1)*3] mass=r*r*r/1000 radius=r charge=r-(MAX_BUBBLE_RADIUS-MIN_BUBBLE_RADIUS)/2
        !distance fixed=RADIUS-r from=i to=:centre strength=100
    !drag strength=0.0001
    !collision strength=200*ease_in

!window size=SIZE linear=true colorbits=16
    !record filename=('blobs.m4v' if RECORD and CYCLE > 1) codec=:hevc crf=20 limit=30
        @bloom_filter size=SIZE/2 radius=hypot(SIZE)/300
            !canvas3d viewpoint=0;0;1200 focus=0 near=1 far=2000 fov=60/360 samples=4
                !light color=2;0;0
                !light color=-1.9;0;0 direction=0;0;-1
                !transform rotate_x=ROT/2 rotate_y=ROT/2
                    !sphere subdivisions=3 color=0.01 size=RADIUS position=$(:bubble;:centre) transparency=0.05
                    for i in ..NBUBBLES
                        !sphere subdivisions=3 position=$(:bubble;i) color=bubble_brightness[i] size=bubble_radii[i]
