
%pragma tempo 60

-- flitter examples/physics.fl --verbose --lockstep --fps=60 --define RECORD=1

import bloom_filter from 'bloom.fl'

let SIZE=(1080;1080) * (1 if RECORD else 2)
    CYCLE=beat/30
    NBUBBLES=500
    RADIUS=250+250*sine(0.5+CYCLE*2)
    ROT=sine(CYCLE)
    NOISE_T=CYCLE*10
    NOISE_SCALE=1/5
    MIN_BUBBLE_RADIUS=5
    MAX_BUBBLE_RADIUS=50
    START=(beta(:start)[..3*NBUBBLES]-0.5)*RADIUS*2

let bubble_radii=MIN_BUBBLE_RADIUS+(1+noise(:radii, i*NOISE_SCALE, NOISE_T))/2*(MAX_BUBBLE_RADIUS-MIN_BUBBLE_RADIUS) for i in ..NBUBBLES
    bubble_brightness=(1+noise(:brightness, i*NOISE_SCALE, NOISE_T)) for i in ..NBUBBLES

!physics dimensions=3 state=:bubble
    --
    -- Force is determined by:
    --   !distance      - strength x |distance - min/max/fixed|
    --   !drag          - strength x speed^2 x radius^2
    --   !collision     - strength x |distance - (radius_1 + radius_2)|
    --   !electrostatic - strength x |charge_1 x charge_2| / distance^2
    --   !gravity       - strength x mass_1 x mass_2 / distance^2
    --   !constant      - strength
    --
    -- Particle accelleration = force / mass
    -- Collisions are *elastic* and are the primary source of simulation instability
    -- Time is measured in beats, so velocity is distance units per beat
    -- An anchor ignored all forces applied to it, but can contribute to forces
    -- on other particles (i.e., it can have gravity, charge, etc.)
    --
    let ease_in=linear(CYCLE)  -- ease in collision strength at the beginning to reduce instability
    !anchor id=:centre position=0;0;0
    for i in ..NBUBBLES
        let r=bubble_radii[i]
        !particle id=i position=START[i*3..(i+1)*3] mass=r*r*r/1000 radius=r charge=r-(MAX_BUBBLE_RADIUS-MIN_BUBBLE_RADIUS)/2
        !distance fixed=RADIUS-r from=i to=:centre strength=100
    !drag strength=0.0001
    !collision strength=200*ease_in
    -- !electrostatic strength=10000*ease_in max_distance=MAX_BUBBLE_RADIUS*5
    -- !constant direction=0;0;-1 strength=1000
    -- !gravity strength=1000*ease

!window size=SIZE linear=true colorbits=16
    !record filename=('blobs.mp4' if RECORD and CYCLE > 1) codec=:hevc crf=20 limit=30
        @bloom_filter radius=hypot(SIZE)/300 size=SIZE/2
            !canvas3d viewpoint=0;0;1200 focus=0 near=1 far=2000 fov=60/360 samples=4
                !light color=2;0;0
                !light color=-1.9;0;0 direction=0;0;-1
                !transform rotate_x=ROT/2 rotate_y=ROT/2
                    !sphere subdivisions=3 color=0.05 size=RADIUS position=$(:bubble;:centre) transparency=0.1
                    for i in ..NBUBBLES
                        !sphere subdivisions=3 position=$(:bubble;i) color=bubble_brightness[i] size=bubble_radii[i]
