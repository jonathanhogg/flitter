
import bloom_filter from 'common/filters.fl'

let SIZE=1080;1080
    CYCLE=($(:bubble;:clock) or 0)/15
    NBUBBLES=500
    RADIUS=250+250*sine(0.5+CYCLE*2)
    NOISE_T=CYCLE*10
    NOISE_SCALE=1/5
    MIN_BUBBLE_RADIUS=5
    MAX_BUBBLE_RADIUS=50
    START=(beta(:start)[..3*NBUBBLES]-0.5)*RADIUS*2

let bubble_radii=MIN_BUBBLE_RADIUS+(1+noise(:radii, i*NOISE_SCALE, NOISE_T))/2*(MAX_BUBBLE_RADIUS-MIN_BUBBLE_RADIUS) for i in ..NBUBBLES
    bubble_brightness=(1+noise(:brightness, i*NOISE_SCALE, NOISE_T)) for i in ..NBUBBLES

!physics dimensions=3 state=:bubble
    !anchor id=:centre position=(-SIZE*(($:pointer)-0.5);0) if $(:pointer;0) else (0;0;0)
    for i in ..NBUBBLES
        let r=bubble_radii[i]
        !particle id=i position=START[i*3..(i+1)*3] radius=r mass=r**3/1000
        !distance from=i to=:centre strength=200 fixed=(r if $(:collapse) else RADIUS-r)
    !drag strength=0.0001
    !collision strength=200 ease=15

!window size=SIZE
    !record filename=(OUTPUT if round(60*beat/tempo*fps) == 9)
        @bloom_filter radius=hypot(SIZE)/150
            !canvas3d viewpoint=0;0;-1200 focus=0 near=1 far=2000 fov=60/360 samples=4
                !light color=2;0;0
                !light color=-1.9;0;0 direction=0;0;1
                !sphere color=0.01 size=RADIUS position=$(:bubble;:centre) transparency=0.05
                for i in ..NBUBBLES
                    !sphere position=$(:bubble;i) color=bubble_brightness[i] size=bubble_radii[i]

    -- press the space bar to collapse the particles into the centre
    !key name=:space state=:collapse
    -- click and move mouse to move the anchor point around
    !pointer state=:pointer
