
-- Texture mapping
--
-- Spheres are mapped to a texture with the equirectangular projection. This is
-- a pretty dumb projection, but it's simple to understand and easy to find
-- appropriate images.
--
-- https://en.wikipedia.org/wiki/Equirectangular_projection
--
-- The relative sizes and orbit timings are roughly correct, but the moon is
-- not really anywhere near this close to Earth.
--
-- The stars sphere is inverted so that that inside surface of it is rendered.
-- The Earth and Moon spheres are placed in a separate group with their lights
-- so that the star sphere isn't affected by them.

%pragma tempo 60

let SIZE=1920;1080
    days=(beat+25)/60

!offscreen
    !adjust id=:stars size=2048;1024 exposure=1 highlights=4 composite=:multiply
        !image filename='assets/stars.jpg'
        !noise scale=0.02 z=beat*100 components=1
    !adjust id=:earth size=2048;1024 exposure=1 shadows=2
        !image filename='assets/earth.jpg'
    !adjust id=:earth_night size=2048;1024 exposure=1
        !image filename='assets/earth-night.jpg'
    !image id=:earth_reflection filename='assets/earth-reflection.jpg'
    !image id=:earth_cloudmap filename='assets/earth-cloudmap.jpg'
    !image id=:earth_cloudmap_transparency filename='assets/earth-cloudmap-transparency.jpg'
    !image id=:moon filename='assets/moon.jpg'

!window size=SIZE
    !record filename=(OUTPUT if frame == run_time * fps - 1)
        !adjust tonemap=:aces
            !bloom radius=hypot(SIZE)/100
                !flare
                    !canvas3d samples=4 viewpoint=0;-25;0 up=0;0;1 near=1 far=100 rotate_y=0.025
                        !sphere size=20 invert=true emissive_texture_id=:stars rotation=0;0;days/365.256
                        !group
                            !light color=0.05
                            !transform rotate_z=days/365.256
                                !light color=3 direction=1;0;0
                            !transform rotate_y=23.44/360 rotate_z=days
                                !material texture_id=:earth emissive_texture_id=:earth_night roughness_texture_id=:earth_reflection
                                    !sphere size=6.371 rotation=0;0;0.25
                                !material texture_id=:earth_cloudmap transparency_texture_id=:earth_cloudmap_transparency
                                    !sphere size=6.421 rotation=0;0;days/14 roughness=0.5
                            !transform rotate_y=-5.14/360 rotate_z=days/27.322 translate=20;0;0
                                !material texture_id=:moon roughness=0.5
                                    !sphere size=1.7375 rotation=0;(5.14-6.68)/360;0
