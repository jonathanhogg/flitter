
import bloom_filter from 'bloom.fl'

let SIZE=1080;1080
    NBALLS=100
    RADII=(beta(:radii)[..NBALLS]+0.5)*50

!physics dimensions=3 state=:balls
    for i in ..NBALLS
        let start=(uniform(:pos;i)[..3]-0.5)*1000+(0;0;500)
            velocity=normal(:xy;i)[..2]*500;2000*beta(:z)[i]
        !particle id=i position=start velocity=velocity radius=RADII[i] mass=RADII[i]**2
    !drag strength=1e-5
    !barrier position=0;0;0 normal=0;0;1 restitution=0.95
    !barrier position=0;0;1000 normal=0;0;-1 restitution=0.95
    !barrier position=-500;0;0 normal=1;0;0 restitution=0.95
    !barrier position=500;0;0 normal=-1;0;0 restitution=0.95
    !barrier position=0;-500;0 normal=0;1;0 restitution=0.95
    !barrier position=0;500;0 normal=0;-1;0 restitution=0.95
    !constant acceleration=0;0;-2000
    !collision strength=100k

!window size=SIZE colorbits=16
    @bloom_filter radius=hypot(SIZE)/200
        !canvas3d viewpoint=1000;-2000;1500 focus=-100;0;500 up=0;0;1 near=1 far=5000 fov=50/360 max_lights=NBALLS samples=4 samples=4
            !light color=50M direction=-1;1;-5 position=1000;-1000;5000 outer=30/360
            !material color=0.01
                !cylinder size=5000;5000;0.01
            !material shininess=200
                for i in ..NBALLS
                    let color=hsv(uniform(:hue)[i];1;1)
                    !transform translate=$(:balls;i)
                        !sphere position=0 size=RADII[i] emissive=color
                        !light position=0 color=color*RADII[i]*500
            !material transparency=0.9 color=0.01 shininess=10
                !box size=999 position=0;0;500 flat=true invert=true
                !box size=1000 position=0;0;500 flat=true
