
import bloom_filter from 'common/bloom.fl'

let SIZE=1080;1080
    NBALLS=120
    RADII=(beta(:radii)[..NBALLS]+0.5)*5

!physics dimensions=3 state=:balls
    for i in ..NBALLS
        let start=(uniform(:pos;i)[..3]-0.5)*100+(0;0;50)
            velocity=normal(:xy;i)[..2]*50;200*beta(:z)[i]
        !particle id=i position=start velocity=velocity radius=RADII[i] mass=RADII[i]**2/10
    !drag strength=50u
    !barrier position=0;0;0 normal=0;0;1 restitution=0.95
    !barrier position=0;0;100 normal=0;0;-1 restitution=0.95
    !barrier position=-50;0;0 normal=1;0;0 restitution=0.95
    !barrier position=50;0;0 normal=-1;0;0 restitution=0.95
    !barrier position=0;-50;0 normal=0;1;0 restitution=0.95
    !barrier position=0;50;0 normal=0;-1;0 restitution=0.95
    !constant acceleration=0;0;-200
    !collision strength=100 power=2

!window size=SIZE colorbits=16
    @bloom_filter radius=hypot(SIZE)/200
        !canvas3d samples=4 viewpoint=100;200;150 focus=-10;0;50 up=0;0;1 far=500 fog_min=200 fog_max=500 fov=50/360
            !light color=0.3
            !light color=8M focus=0 position=1000;-1000;2000 outer=5/360
            !material color=0.01
                !cylinder size=5000;5000;0.01
            !material roughness=0.4
                for i in ..NBALLS
                    let color=hsv(uniform(:hue)[i];0.95;1)
                    !transform translate=$(:balls;i)
                        !sphere position=0 size=RADII[i] color=color
            !material transparency=0.99 color=1 roughness=0.1
                !box size=99.9 position=0;0;50 invert=true
                !box size=100 position=0;0;50
