
%import common.ESCAPED_STRING
%import common.WS_INLINE
%ignore WS_INLINE
%ignore /\\$\n/m
%declare _INDENT _DEDENT


_NL: /(\r?\n[\t ]*)+/

NAME : /[_a-z][_a-z0-9]*/i
SIGNED_NUMBER : /([0-9]+(\.[0-9]+)?|\.[0-9]+)(e[-+]?[0-9]+)?/i
QUERY : /{[^}]+}/


sequence : _NL? expression+

?expression : node _NL
            | node "^" _NL _INDENT sequence _DEDENT -> prepend
            | node _NL _INDENT sequence _DEDENT -> append
            | "for" NAME "in" range _NL _INDENT sequence _DEDENT -> loop
            | "let" bindings _NL -> let
            | "if" tests ["else" _NL _INDENT sequence _DEDENT] -> if_else
            | "%pragma" NAME node _NL -> pragma

tests : test ("elif" test)*

test : composition _NL _INDENT sequence _DEDENT

bindings : binding+ -> tuple

binding : NAME "=" composition

?node : composition
      | "!" NAME tags -> node
      | node NAME "=" composition -> attribute

?composition : comprehension
             | comprehension (";" comprehension)+ -> sequence

?comprehension : or
               | "for" NAME "in" or "|" comprehension -> loop
               | "let" inline_bindings "|" comprehension -> inline_let

inline_bindings: binding+ -> tuple

inline_binding: NAME "=" or -> binding

?or : and
    | or "or" and -> logical_or

?and : not
     | and "and" not -> logical_and

?not : comparison
     | "not" not -> logical_not

?comparison : range
            | comparison "==" range -> eq
            | comparison "!=" range -> ne
            | comparison "<" range -> lt
            | comparison ">" range -> gt
            | comparison "<=" range -> le
            | comparison ">=" range -> ge

?range : sum
       | [sum] ".." sum [":" sum] -> range

?sum : product
     | sum "+" product -> add
     | sum "-" product -> subtract

?product : power
         | product "*" power -> multiply
         | product "/" power -> divide
         | product "//" power -> floor_divide
         | product "%" power -> modulo

?power : atom
       | atom "**" power -> power

?atom : literal
      | NAME -> name
      | "-" atom -> neg
      | atom "(" args ")" -> call
      | atom "[" node "]" -> slice
      | QUERY -> search
      | "$" atom -> lookup
      | "(" node ")"

literal : SIGNED_NUMBER
        | ESCAPED_STRING

args : (node ("," node)*)? -> tuple

tags: tag*

?tag: "#" NAME
